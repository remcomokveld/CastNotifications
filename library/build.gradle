apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    publishNonDefault true

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "FIREBASE_TOKEN", "\""+System.getenv("FIREBASE_TOKEN")+"\"" ?: "\"\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "gms", "support"

    productFlavors {

        gms84 {
            dimension "gms"
        }
        gms83 {
            dimension "gms"
        }
        gms75 {
            dimension "gms"
        }

        support2311 {
            dimension "support"
        }

    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    support2311Compile 'com.android.support:appcompat-v7:23.1.1'
    support2311Compile 'com.android.support:mediarouter-v7:23.1.1'
    gms84Compile 'com.google.android.gms:play-services-cast:8.4.0'
    gms83Compile 'com.google.android.gms:play-services-cast:8.3.0'
    gms75Compile 'com.google.android.gms:play-services-cast:7.5.0'

    compile project(':firebase_logging')
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.3.6'
    }
}

apply plugin: 'com.novoda.bintray-release' // must be applied after your artifact generating plugin (eg. java / com.android.library)

publish {
    userOrg = 'rmokveld'
    groupId = 'nl.rmokveld'
    artifactId = 'cast-notification'
    publishVersion = "0.2.0"
    desc = 'Library to allow casting directly from a notification'
    website = 'https://github.com/remcomokveld/CastNotifications'

    bintrayUser = System.getenv("BINTRAY_USER")
    bintrayKey = System.getenv("BINTRAY_KEY")
    dryRun = false
}